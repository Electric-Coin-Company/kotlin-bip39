import cash.z.ecc.android.Deps

/////////////////////////////////////////
// Publishing
/////////////////////////////////////////

group = Deps.group
version = Deps.versionName

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT-style"
            url "http://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "gmale"
            name "Kevin Gorham"
            email "kevin.gorham@z.cash"
        }
    }

    scm {
        url Deps.githubUrl
    }
}

// Jar containing Kotlin sources
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from kotlin.sourceSets.main.kotlin.srcDirs
}

// Jar containing docs
task docsJar(type: Jar) {
    archiveClassifier = "javadoc"
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    dependsOn dokka
    from dokka
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact docsJar
            groupId Deps.group
            artifactId Deps.artifactName
            version Deps.versionName

            pom.withXml {
                def root = asNode()
                root.appendNode('description', Deps.description)
                root.appendNode('name', Deps.artifactName)
                root.appendNode('url', )
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    override = true
    pkg {
        repo = 'android'
        name = Deps.artifactName
        description = Deps.description
        publish = true
        publicDownloadNumbers = true
        userOrg = 'ecc-mobile'
        licenses = ['MIT']
        vcsUrl = Deps.githubUrl
        dryRun = true
        version {
            name = Deps.versionName
            desc = Deps.description
            released = new Date()
            vcsTag = this.version
        }
    }
}
